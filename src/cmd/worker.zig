const std = @import("std");
const posthog = @import("posthog");

const db = @import("../db/pool.zig");
const worker_config = @import("worker_config.zig");
const env_vars = @import("../config/env_vars.zig");
const events_bus = @import("../events/bus.zig");
const worker = @import("../pipeline/worker.zig");
const git_ops = @import("../git/ops.zig");
const obs_log = @import("../observability/logging.zig");
const posthog_events = @import("../observability/posthog_events.zig");
const error_codes = @import("../errors/codes.zig");
const langfuse = @import("../observability/langfuse.zig");
const common = @import("common.zig");

const log = std.log.scoped(.worker);

var shutdown_requested = std.atomic.Value(bool).init(false);

fn onSignal(sig: i32) callconv(.c) void {
    _ = sig;
    shutdown_requested.store(true, .release);
}

fn installSignalHandlers() void {
    const action = std.posix.Sigaction{
        .handler = .{ .handler = onSignal },
        .mask = std.posix.sigemptyset(),
        .flags = 0,
    };
    std.posix.sigaction(std.posix.SIG.INT, &action, null);
    std.posix.sigaction(std.posix.SIG.TERM, &action, null);
}

fn signalWatcher(wstate: *worker.WorkerState) void {
    while (!shutdown_requested.load(.acquire)) {
        std.Thread.sleep(100 * std.time.ns_per_ms);
    }
    wstate.running.store(false, .release);
}

pub fn run(alloc: std.mem.Allocator) !void {
    log.info("phase=worker status=start", .{});

    log.info("phase=env_check status=start", .{});
    env_vars.enforceFromEnvWithMode(alloc, .worker) catch |err| {
        switch (err) {
            env_vars.EnvVarsErrors.MissingDatabaseUrlWorker => log.err("phase=env_check status=fail err=DATABASE_URL_WORKER not set", .{}),
            env_vars.EnvVarsErrors.MissingRedisUrlWorker => log.err("phase=env_check status=fail err=REDIS_URL_WORKER not set", .{}),
            env_vars.EnvVarsErrors.RedisWorkerTlsRequired => log.err("phase=env_check status=fail err=REDIS_URL_WORKER must use rediss://", .{}),
            else => log.err("phase=env_check status=fail err={s}", .{@errorName(err)}),
        }
        std.process.exit(1);
    };
    log.info("phase=env_check status=ok", .{});

    log.info("phase=config_load status=start", .{});
    var worker_cfg = worker_config.Config.load(alloc) catch |err| {
        switch (err) {
            worker_config.ValidationError.MissingGitHubAppId,
            worker_config.ValidationError.MissingGitHubAppPrivateKey,
            worker_config.ValidationError.InvalidMaxAttempts,
            worker_config.ValidationError.InvalidWorkerConcurrency,
            worker_config.ValidationError.InvalidRunTimeoutMs,
            worker_config.ValidationError.InvalidRateLimitCapacity,
            worker_config.ValidationError.InvalidRateLimitRefillPerSec,
            => {
                worker_config.printValidationError(@errorCast(err));
                log.err("phase=config_load status=fail err={s}", .{@errorName(err)});
            },
            else => log.err("phase=config_load status=fail err={s}", .{@errorName(err)}),
        }
        std.process.exit(1);
    };
    defer worker_cfg.deinit();
    log.info("phase=config_load status=ok", .{});

    if (langfuse.configFromEnv(alloc)) |cfg| {
        langfuse.installAsyncExporter(alloc, cfg) catch |err| {
            alloc.free(cfg.host);
            alloc.free(cfg.public_key);
            alloc.free(cfg.secret_key);
            obs_log.logWarnErr(.worker, err, "langfuse async exporter install failed; continuing with fallback mode", .{});
        };
        if (langfuse.isAsyncExporterInstalled()) {
            defer langfuse.uninstallAsyncExporter();
            log.info("langfuse async exporter enabled", .{});
        }
    }

    const posthog_api_key = std.process.getEnvVarOwned(alloc, "POSTHOG_API_KEY") catch null;
    defer if (posthog_api_key) |key| alloc.free(key);
    const ph_client: ?*posthog.PostHogClient = if (posthog_api_key) |key| blk: {
        break :blk posthog.init(alloc, .{
            .api_key = key,
            .host = "https://us.i.posthog.com",
            .flush_interval_ms = 10_000,
            .flush_at = 20,
            .max_retries = 3,
        }) catch |err| {
            obs_log.logWarnErr(.worker, err, "posthog init failed; analytics disabled", .{});
            break :blk null;
        };
    } else null;
    defer if (ph_client) |client| client.deinit();

    log.info("phase=db_connect role=worker status=start", .{});
    const worker_pool = db.initFromEnvForRole(alloc, .worker) catch |err| {
        log.err("phase=db_connect role=worker status=fail err={s}", .{@errorName(err)});
        posthog_events.trackStartupFailed(ph_client, "worker", "db_connect", @errorName(err), error_codes.ERR_STARTUP_DB_CONNECT);
        if (ph_client) |c| c.deinit();
        std.process.exit(1);
    };
    defer worker_pool.deinit();
    log.info("phase=db_connect role=worker status=ok", .{});

    log.info("phase=migration_check status=start", .{});
    common.enforceServeMigrationSafety(worker_pool, false) catch |err| {
        switch (err) {
            common.MigrationGuardError.MigrationPending => log.err(
                "phase=migration_check status=fail err=pending_migrations hint=run zombied migrate before worker startup",
                .{},
            ),
            common.MigrationGuardError.MigrationFailed => log.err(
                "phase=migration_check status=fail err=migration_failure_state hint=inspect schema_migration_failures then rerun zombied migrate",
                .{},
            ),
            common.MigrationGuardError.MigrationSchemaAhead => log.err(
                "phase=migration_check status=fail err=schema_ahead hint=deploy matching binary",
                .{},
            ),
            else => log.err("phase=migration_check status=fail err={s}", .{@errorName(err)}),
        }
        posthog_events.trackStartupFailed(ph_client, "worker", "migration_check", @errorName(err), error_codes.ERR_STARTUP_MIGRATION_CHECK);
        if (ph_client) |c| c.deinit();
        std.process.exit(1);
    };
    log.info("phase=migration_check status=ok", .{});

    std.fs.makeDirAbsolute(worker_cfg.cache_root) catch |err| switch (err) {
        error.PathAlreadyExists => {},
        else => obs_log.logWarnErr(.worker, err, "could not create cache root {s}", .{worker_cfg.cache_root}),
    };
    {
        const stats = git_ops.cleanupRuntimeArtifacts(alloc, worker_cfg.cache_root, "/tmp");
        log.info(
            "worker cleanup startup removed_worktrees={d} failed_worktrees={d} pruned_bare={d} failed_prunes={d}",
            .{ stats.removed_worktrees, stats.failed_worktree_removals, stats.pruned_bare_repos, stats.failed_bare_prunes },
        );
    }

    var wstate = worker.WorkerState.init();
    shutdown_requested.store(false, .release);
    installSignalHandlers();

    var event_bus = events_bus.Bus.init();
    events_bus.install(&event_bus);
    defer events_bus.uninstall();

    const thread_count: usize = @max(@as(usize, @intCast(worker_cfg.worker_concurrency)), 1);
    var worker_threads = try alloc.alloc(std.Thread, thread_count);
    defer alloc.free(worker_threads);

    var spawned_workers: usize = 0;
    var signal_thread: ?std.Thread = null;
    var event_thread: ?std.Thread = null;
    errdefer {
        wstate.running.store(false, .release);
        shutdown_requested.store(true, .release);
        event_bus.stop();
        if (signal_thread) |*t| t.join();
        if (event_thread) |*t| t.join();
        while (spawned_workers > 0) {
            spawned_workers -= 1;
            worker_threads[spawned_workers].join();
        }
    }

    const pipeline_cfg = worker.WorkerConfig{
        .pool = worker_pool,
        .config_dir = worker_cfg.config_dir,
        .cache_root = worker_cfg.cache_root,
        .github_app_id = worker_cfg.github_app_id,
        .github_app_private_key = worker_cfg.github_app_private_key,
        .pipeline_profile_path = worker_cfg.pipeline_profile_path,
        .max_attempts = worker_cfg.max_attempts,
        .run_timeout_ms = worker_cfg.run_timeout_ms,
        .rate_limit_capacity = worker_cfg.rate_limit_capacity,
        .rate_limit_refill_per_sec = worker_cfg.rate_limit_refill_per_sec,
        .posthog = ph_client,
    };

    for (worker_threads) |*t| {
        t.* = try std.Thread.spawn(.{}, worker.workerLoop, .{ pipeline_cfg, &wstate });
        spawned_workers += 1;
    }
    signal_thread = try std.Thread.spawn(.{}, signalWatcher, .{&wstate});
    event_thread = try std.Thread.spawn(.{}, events_bus.runThread, .{&event_bus});

    log.info("worker threads started concurrency={d}", .{thread_count});
    posthog_events.trackWorkerStarted(ph_client, @intCast(thread_count));

    for (worker_threads) |*t| t.join();
    shutdown_requested.store(true, .release);
    event_bus.stop();
    if (signal_thread) |*t| t.join();
    if (event_thread) |*t| t.join();

    {
        const stats = git_ops.cleanupRuntimeArtifacts(alloc, worker_cfg.cache_root, "/tmp");
        log.info(
            "worker cleanup shutdown removed_worktrees={d} failed_worktrees={d} pruned_bare={d} failed_prunes={d}",
            .{ stats.removed_worktrees, stats.failed_worktree_removals, stats.pruned_bare_repos, stats.failed_bare_prunes },
        );
    }
}
